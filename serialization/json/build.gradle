plugins 
{
    id 'java-library'
    id 'eclipse'
}

if(System.getProperty("PUBLISH_SCRIPT_PATH"))
	apply from: System.getProperty("PUBLISH_SCRIPT_PATH")

group = 'jadex'

sourceCompatibility = '1.17'

repositories
{
    mavenCentral()
}

dependencies
{
	def v = System.getProperty('JADEX_VERSION')?:'+'
	implementation 'jadex:traverser:'+v
	implementation 'jadex:collection:'+v
	implementation 'jadex:common:'+v
	implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
	testImplementation testFixtures('jadex:traverser:'+v)
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
  useJUnitPlatform()
}

// Hack for broken test fixtures in eclipse
// cf. https://github.com/eclipse/buildship/issues/1206#issuecomment-1367742986
eclipse.classpath {
    file.whenMerged { classpath ->
        classpath.entries.findAll { entry -> entry instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency && entry.entryAttributes.test }
        	.each { it.entryAttributes['test'] = 'false' }
	}
}

task copyDependencies {
    doLast {
        copy {
            configurations.compileClasspath.each { File file ->
                  if (!file.absolutePath.startsWith(project.rootDir.absolutePath)) {
                    if (file.name.endsWith('.jar'))
                        from file.getParent()
                }
            } 
            into 'build/libs/external-dependencies'
        }
    }
    outputs.upToDateWhen { false }
}
build.dependsOn(copyDependencies)