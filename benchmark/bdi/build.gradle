plugins 
{
    id 'java-library'
}

description = 'Benchmark tests for bdi agents'

group = 'org.activecomponents.jadex'

if(System.getProperty("PUBLISH_SCRIPT_PATH"))
	apply from: System.getProperty("PUBLISH_SCRIPT_PATH")

java
{
	sourceCompatibility = '1.21'
}

repositories
{
    mavenCentral()
}

dependencies
{
	def v = System.getProperty('JADEX_VERSION')?:'+'
	implementation 'org.activecomponents.jadex:common:'+v
	implementation 'org.activecomponents.jadex:core:'+v
	implementation 'org.activecomponents.jadex:execution:'+v
	implementation 'org.activecomponents.jadex:logbase:'+v
	implementation 'org.activecomponents.jadex:injection:'+v
	implementation 'org.activecomponents.jadex:bdi:'+v
	implementation 'org.activecomponents.jadex:benchmark-core:'+v
	implementation 'org.activecomponents.jadex:application-bdi:'+v
	implementation 'org.activecomponents.jadex:environment:'+v
	
	implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'

	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
	// required for gradle 9!? https://docs.gradle.org/8.7/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
  useJUnitPlatform()
  exclude 'jadex/benchmark/**'
}

task benchmark(type : Test) {
  useJUnitPlatform()
  include 'jadex/benchmark/**'
  minHeapSize = "4g"
  maxHeapSize = "4g"
  enableAssertions = false
  
  // https://docs.gradle.org/8.10.2/userguide/upgrading_version_8.html#test_task_default_classpath
  testClassesDirs = testing.suites.test.sources.output.classesDirs
  classpath = testing.suites.test.sources.runtimeClasspath
}
